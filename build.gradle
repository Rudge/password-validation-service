group 'com.rudge.tech'
version '1.0-SNAPSHOT'

ext {
    javalin_version = "3.8.0"
    jackson_version = "2.10.3"
    slf4j_version = "1.7.30"
    koin_version = "2.1.5"
    junit_version = "5.6.1"
    rest_assured_version = "4.3.0"
    swagger_version = "3.25.0"
    mockk_version = "1.10.0"
}

buildscript {
    ext.kotlin_version = "1.3.72"

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jacoco'
mainClassName = "com.rudge.tech.password.app.AppKt"

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    componentTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

test() {
    useJUnitPlatform()
}

task componentTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.componentTest.output.classesDirs
    classpath = sourceSets.componentTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn componentTest
test.finalizedBy jacocoTestReport

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.javalin:javalin:$javalin_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "org.slf4j:slf4j-simple:$slf4j_version"
    implementation "org.koin:koin-core:$koin_version"
    api "org.webjars:swagger-ui:$swagger_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"

    componentTestImplementation sourceSets.main.compileClasspath
    componentTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    componentTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    componentTestRuntime "org.junit.jupiter:junit-jupiter:$junit_version"
    componentTestImplementation "io.rest-assured:kotlin-extensions:$rest_assured_version"
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        html.enabled true
        html.destination file("${buildDir}/jacoco")
    }
}
