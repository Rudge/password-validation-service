group 'com.rudge.tech'
version '1.0-SNAPSHOT'

ext {
    javalin_version = "3.8.0"
    jackson_version = "2.10.3"
    slf4j_version = "1.7.30"
    koin_version = "2.1.5"
    junit_version = "5.6.1"
    unirest_version = "3.7.02"
    swagger_version = "3.25.0"
}

buildscript {
    ext.kotlin_version = "1.3.72"

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
mainClassName = "com.rudge.tech.password.app.AppKt"

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    componentTest {
        kotlin.srcDir file('src/componentTest/kotlin')
        resources.srcDir file('src/componentTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.testRuntime
    }
}

test() {
    useJUnitPlatform()
}

task componentTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.componentTest.output.classesDirs
    classpath = sourceSets.componentTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn componentTest
componentTest.mustRunAfter test

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.javalin:javalin:$javalin_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "org.slf4j:slf4j-simple:$slf4j_version"
    implementation "org.koin:koin-core:$koin_version"
    api "org.webjars:swagger-ui:$swagger_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"

    componentTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    componentTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    componentTestRuntime "org.junit.jupiter:junit-jupiter:$junit_version"
    componentTestImplementation "com.konghq:unirest-java:$unirest_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
